package rules;

import com.tcc.epidemiologia.domain.SinaisVitais;
import com.tcc.epidemiologia.domain.Alerta;
import com.tcc.epidemiologia.domain.EventoClinico;
import java.time.LocalDateTime;

import com.tcc.epidemiologia.service.BairroService;
import com.tcc.epidemiologia.infrastructure.EventCache;
import java.time.Instant
import java.util.Date
global BairroService bairroService;
global EventCache eventCache;

dialect "java";

declare SinaisVitais
    @role( event )
    @timestamp( timestamp  )
    @expires( 60s )
end

declare EventoClinico
    @role( event )
    @timestamp( timestamp )
    @expires( 29d )
end

// Limiar Epidemico baseado em desvio padrao com intervalo de confiança de 95%
function boolean hasAumentoSignificativo(double s0, double s1, double s2, double s3) {
    double media = (s1 + s2 + s3) / 3.0;
    double desvio = Math.sqrt(
            (Math.pow(s1 - media, 2) +
                    Math.pow(s2 - media, 2) +
                    Math.pow(s3 - media, 2)) / 3.0
    );
    return s0 > media + 1.96 * desvio;
}

global Integer MINIMO_CASOS;

// 315.0 é um valor considerado "moderado" sendo estágio inicial de lesão no pulmão.
// 235.0 é um valor considerado "grave" sugere um quadro de Síndrome do Desconforto Respiratório Agudo

rule "SRAG - Suspeita (Febre + achado respiratório)"
    activation-group "SRAG"
    salience 20
    when
        $e: SinaisVitais(
                temperatura >= 38.0,
                $bairro : codigoBairro,
                eval(
                    (spo2 <= 93.0)
                    ||
                    // (2) S/F ≤ 315 (usar S/F só entre 80–97%)
                    (
                        (spo2 >= 80.0 && spo2 <= 97.0) &&
                        ((spo2 / (o2Percent/100.0)) <= 315.0)
                    )
                    ||
                    // (3) FR ≥ 30/min
                    (frequenciaRespiratoria >= 30.0)
                )
            ) from entry-point "entrada-sinais-vitais"
    then
        insert(EventoClinico.create("SRAG", $bairro, $e.timestamp()));
        eventCache.insert($e.id());
end

rule "SRAG - Suspeita (SpO2 baixa + FR alta)"
    activation-group "SRAG"
    salience 15
    when
        $e : SinaisVitais(
            frequenciaRespiratoria >= 30.0,
            $bairro : codigoBairro,
            eval(
                (spo2 <= 93.0)
                ||
                // (2) S/F ≤ 315 (válido 80–97%)
                (
                    (spo2 >= 80.0 && spo2 <= 97.0) &&
                    ((spo2 / (o2Percent/100.0)) <= 315.0)
                )
            )
        ) from entry-point "entrada-sinais-vitais"
    then
        insert(EventoClinico.create("SRAG", $bairro, $e.timestamp()));
        eventCache.insert($e.id());
end

rule "SRAG - Suspeita Reforçada"
    activation-group "SRAG"
    salience 10
    when
        $e : SinaisVitais(
            temperatura >= 38.0,
            frequenciaRespiratoria >= 30.0,
            $bairro : codigoBairro,
            eval(
                (
                    (spo2 <= 93.0)
                )
                ||
                // (2) S/F ≤ 235 (proxy de maior gravidade; válido 80–97%)
                (
                    (spo2 >= 80.0 && spo2 <= 97.0) &&
                    ((spo2 / (o2Percent/100.0)) <= 235.0)
                )
            )
        ) from entry-point "entrada-sinais-vitais"
    then
        insert(EventoClinico.create("SRAG", $bairro, $e.timestamp()));
        eventCache.insert($e.id());
end

// ------------------------------------------------------------------------------------------------------ //

rule "Possível Surto de SRAG"
    when
        $ref : EventoClinico( tipo == "SRAG" )
        not( Alerta( tipo == "Possível Surto de Síndrome Respiratoria Aguda Grave", bairroCodigo == $ref.codigoBairro ) )
        $bairro : Long() from $ref.codigoBairro

        Number( $semanaAtual : intValue ) from accumulate(
            EventoClinico( tipo == "SRAG", codigoBairro == $bairro, timestamp before[0d, 7d] $ref.timestamp ),
            count(1)
        )
        Number( $semanaAnterior : intValue ) from accumulate(
            EventoClinico( tipo == "SRAG", codigoBairro == $bairro, timestamp before[7d, 14d] $ref.timestamp ),
            count(1)
        )
        Number( $semanaRetrasada : intValue ) from accumulate(
            EventoClinico( tipo == "SRAG", codigoBairro == $bairro, timestamp before[14d, 21d] $ref.timestamp ),
            count(1)
        )
        Number( $semanaAntepassada : intValue ) from accumulate(
            EventoClinico( tipo == "SRAG", codigoBairro == $bairro, timestamp before[21d, 28d] $ref.timestamp ),
            count(1)
        )

        eval( $semanaAnterior + $semanaRetrasada + $semanaAntepassada >= MINIMO_CASOS )
        eval( hasAumentoSignificativo($semanaAtual, $semanaAnterior, $semanaRetrasada, $semanaAntepassada) )
    then
        insert(Alerta.create(
            $ref.getTimestamp(),
            "Possível Surto de Síndrome Respiratoria Aguda Grave",
            bairroService.getNomeByCodigo($bairro),
            $bairro, $semanaAtual, $semanaAnterior, $semanaRetrasada, $semanaAntepassada));
end
